###############################################################################
#
#    Licensed Materials - Property of IBM
#    "Restricted Materials of IBM"
#
#    5724-S44
#
#    (C) Copyright IBM Corp. 2005, 2012
#
#    Netcool Knowledge Library
#
###############################################################################
#
# Draft rules for VMWare ESX5.0/5.1 Integration.
#
###############################################################################
#
# 1.0 - Initial Release.
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-03, Jan 2006)
#
#          -  VMWARE-VC-EVENT-MIB
#
###############################################################################

case ".1.3.6.1.4.1.6876.4.3": ### - Notifications from VMWARE-VC-EVENT-MIB (200912150000Z)

	log(DEBUG, "<<<<< Entering... vmware-VMWARE-VC-EVENT-MIB.include.snmptrap.rules >>>>>")

    @Agent = "vmware-VMWARE-VC-EVENT-MIB"
    @Class = "3360"
    
    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
    
     
        case "201": ### vpxdAlarm
        
			discard
         
        case "202": ### vpxdDiagnostic
        
            ##########
            ##########
            
            
            $OS_EventId = "SNMPTRAP-vmware-VMWARE-VC-EVENT-MIB-vpxdDiagnostic"

            @AlertGroup = "vCenter Server Diagnostic"
            @Summary = "vCenter Server Diagnostic Notification"
            
            $DEFAULT_Severity = 3
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0               
            
            @Identifier = @Node + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap
            
 			details($*)
         
        case "203": ### vpxdAlarmInfo
        
            ##########
            # $1 = vmwVpxdTargetObjType
            # $2 = vmwVpxdOldStatus
            # $3 = vmwVpxdNewStatus
            # $4 = vmwVpxdObjValue
            # $5 = vmwVpxdTargetObj
            ##########
            
            $vmwVpxdTargetObjType = lookup($1, VmwVpxdTargetObjType)
            $vmwVpxdOldStatus = $2
            $vmwVpxdNewStatus = $3
            $vmwVpxdObjValue = $4
            $vmwVpxdTargetObj = $5
            
            $eventgroup = extract($vmwVpxdObjValue, "alarm.([A-Za-z0-9 \*]+) -")

            
            $OS_EventId = "SNMPTRAP-vmware-VMWARE-VC-EVENT-MIB-vpxdAlarmInfo"
            $SEV_KEY = $OS_EventId + "_" + $vmwVpxdNewStatus

	    # If alert group mapping to generic alert groups is not required then
	    # edit this section so that it just contains the commented out line.
            #@AlertGroup = "Entity Alarm State "  + $eventgroup
	    @AlertGroup = lookup($eventgroup, VmwVirtManEventCorrelationAlertGroup)
	    if (match(@AlertGroup,"Entity Alarm State"))
	    {
            	@AlertGroup = @AlertGroup + " " + $eventgroup
	    }
	    # end of alert group section

            @AlertKey = "Object : " + $vmwVpxdTargetObj

            @Node = $vmwVpxdTargetObj

            update(@AlertGroup)
            
            @Summary = "Alarm " + $eventgroup + " for " + $vmwVpxdTargetObj + " State Changed From " + $vmwVpxdOldStatus + " To " + $vmwVpxdNewStatus
            
            $DEFAULT_Severity = 3
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0               
            
            update(@Summary)

            # Set identifier to produce separate events for each state change.  
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $SEV_KEY
            
            $vmwVpxdTargetObjType = $vmwVpxdTargetObjType + " ( " + $1 + " )"
 			details($vmwVpxdTargetObjType, $vmwVpxdOldStatus, $vmwVpxdNewStatus, $vmwVpxdObjValue, $vmwVpxdTargetObj, $eventgroup)
         
        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, vmware-VMWARE-VC-EVENT-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, vmware-VMWARE-VC-EVENT-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/vmware/vmware-VMWARE-VC-EVENT-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/vmware/vmware-VMWARE-VC-EVENT-MIB.user.include.snmptrap.rules"


##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... vmware-VMWARE-VC-EVENT-MIB.include.snmptrap.rules >>>>>")


